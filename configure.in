dnl Process this file with autoconf to produce a configure script.
AC_INIT(version)
AC_CONFIG_HEADER(include/gale/config.h:config.h.in)
AC_CANONICAL_HOST
build_list="lib auth server gsend gsub gwatch gkeys gdomain include/gale"
extra_libs=""
TOP="`pwd`"

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_YACC

dnl System type checks.
case "$host" in
  *-sgi-irix6*)
    LDFLAGS="-Wl,-rpath,$LPATH $LDFLAGS"
    no_wacky_libs=yes
    AC_DEFINE(OS_IRIX)
    ;;
  *-hp-hpux*)
    AC_DEFINE(OS_HPUX)
    ;;
  *-*-solaris2*)
    AC_DEFINE(OS_SOLARIS)
    ;;
  *-*-*bsd*)
    AC_DEFINE(OS_BSD)
    ;;
esac

dnl Checks for libraries.
if test -z "$no_wacky_libs" ; then
  AC_CHECK_LIB(nsl,main)
  AC_CHECK_LIB(socket,socket)
fi
AC_CHECK_LIB(termcap,tgetent)
AC_CHECK_LIB(dl,dlopen)

save_flags="$CPPFLAGS"
CPPFLAGS="-Dmain=cgiMain $CPPFLAGS"
AC_CHECK_LIB(cgic,cgiFormString)
CPPFLAGS="$save_flags"

AC_CHECK_LIB(db,db_open,[
  LIBS="-ldb $LIBS"
  build_list="$build_list glog"
])

AC_CHECK_LIB(pq,PQsetdb,[
  LIBS="-lpq $LIBS"
  build_list="$build_list glsql"
])

AC_CHECK_LIB(zephyr,ZInitialize,[
  LIBS="-lzephyr -lcom_err $LIBS"
  build_list="$build_list gzgw"
  AC_CHECK_LIB(krb,tf_get_pname)
  AC_CHECK_LIB(des,des_key_sched)
],,-lcom_err)

AC_MSG_CHECKING([for local copy of RSAREF source])
if test -d "rsaref/source"; then
  AC_MSG_RESULT(yes)
  build_list="rsaref $build_list"
  extra_libs="$extra_libs \${TOP}/rsaref/build/rsaref.a"
  CPPFLAGS="$CPPFLAGS -I\${TOP}/rsaref/source"
else
  AC_MSG_RESULT(no)
  AC_CHECK_LIB(rsaref,R_RandomInit,[LIBS="$LIBS -lrsaref"],[
    AC_MSG_ERROR(cannot find RSAREF library, see INSTALL directions)
  ])
fi

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h sys/time.h syslog.h unistd.h sys/bitypes.h term.h dlfcn.h)

if test ! -f "rsaref/source/rsaref.h"; then
  AC_CHECK_HEADERS(rsaref.h,,[
    AC_MSG_ERROR(cannot find RSAREF headers, see INSTALL directions)
  ])
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_BIGENDIAN
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(long)

dnl Checks for library functions.
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(mkdir putenv select socket strerror uname)

AC_SUBST(build_list)
AC_SUBST(extra_libs)
AC_OUTPUT(defs)
