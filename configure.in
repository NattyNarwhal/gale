dnl Process this file with autoconf to produce a configure script.
AC_INIT(INSTALL)
AM_INIT_AUTOMAKE(gale,0.17)
AM_CONFIG_HEADER(include/gale/config.h:config.h.in)
AC_CANONICAL_HOST

BUILD="misc lib auth server gsend gsub gwatch gkeys gdomain"
extra_libs=""
no_wacky_libs=""
have_libdb=""

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_YACC
AC_PROG_MAKE_SET

dnl System type checks.
case "$host" in
  *-sgi-irix6*)
    LDFLAGS="-Wl,-rpath,$LPATH $LDFLAGS"
    no_wacky_libs=yes
    AC_DEFINE(OS_IRIX)
    ;;
  *-hp-hpux*)
    AC_DEFINE(OS_HPUX)
    ;;
  *-*-solaris2*)
    AC_DEFINE(OS_SOLARIS)
    ;;
  *-*-*bsd*)
    AC_DEFINE(OS_BSD)
    ;;
esac

dnl Checks for libraries.
if test -z "$no_wacky_libs" ; then
  AC_CHECK_LIB(nsl,main)
  AC_CHECK_LIB(socket,socket)
fi
AC_CHECK_LIB(termcap,tgetent)
AC_CHECK_LIB(dl,dlopen)
AC_CHECK_LIB(readline,readline)

save_flags="$CPPFLAGS"
CPPFLAGS="-Dmain=cgiMain $CPPFLAGS"
AC_CHECK_LIB(cgic,cgiFormString)
CPPFLAGS="$save_flags"

AC_CHECK_LIB(db,db_appinit,[
  LIBS="-ldb $LIBS"
  have_libdb="yes"
])

AC_CHECK_LIB(pq,PQsetdb,[
  LIBS="-lpq $LIBS"
  BUILD="$BUILD glsql"
])

AC_CHECK_LIB(zephyr,ZInitialize,[
  LIBS="-lzephyr -lcom_err $LIBS"
  BUILD="$BUILD gzgw"
  AC_CHECK_LIB(krb,tf_get_pname)
  AC_CHECK_LIB(des,des_key_sched)
],,-lcom_err)

AC_MSG_CHECKING([for local copy of RSAREF source])
if test -f "rsaref/source/rsa.c"; then
  AC_MSG_RESULT(yes)
  BUILD="rsaref $BUILD"
  CPPFLAGS="$CPPFLAGS -I\${top_srcdir}/rsaref/source"
  LDFLAGS="-L\${top_builddir}/rsaref/source $LDFLAGS"
else
  AC_MSG_RESULT(no)
  AC_CHECK_LIB(rsaref,R_RandomInit,[:],[
    AC_MSG_ERROR(cannot find RSAREF library, see INSTALL directions)
  ])
fi

dnl Checks for header files.
AC_CHECK_HEADERS(sys/bitypes.h curses.h term.h dlfcn.h readline/readline.h)

if test -n "$have_libdb"; then
  AC_MSG_CHECKING([for suitable version of Berkeley db])
  AC_TRY_COMPILE([
#include <db.h>
  ],[
    DB_ENV dbenv;
    dbenv.db_errcall;
  ],[
    AC_MSG_RESULT(yes)
    BUILD="$BUILD glog"
  ],AC_MSG_RESULT(no))
fi

if test ! -f "rsaref/source/rsaref.h"; then
  AC_CHECK_HEADERS(rsaref.h,,[
    AC_MSG_ERROR(cannot find RSAREF headers, see INSTALL directions)
  ])
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(long)

dnl Checks for library functions.

dnl Configurations that would break tests.
LIBS="-lgale -lgauth -lgmisc -lrsaref $LIBS"
LDFLAGS="-L\${top_builddir}/lib -L\${top_builddir}/auth -L\${top_builddir}/misc $LDFLAGS"
CPPFLAGS="-I\${top_srcdir}/include -I\${top_builddir}/include $CPPFLAGS"
CPPFLAGS="-DGALE_SYS_DIR=\\\"${sysconfdir}/gale\\\" $CPPFLAGS"
BUILD="$BUILD include"

AC_SUBST(BUILD)
AC_OUTPUT(
	Makefile 
	lib/Makefile 
	auth/Makefile 
	misc/Makefile
	gsub/Makefile
	gzgw/Makefile
	glog/Makefile
	glsql/Makefile
	gsend/Makefile
	gkeys/Makefile
	gwatch/Makefile
	server/Makefile
	gdomain/Makefile
	include/Makefile 
	include/gale/Makefile
	rsaref/Makefile
	rsaref/source/Makefile
)
