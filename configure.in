dnl Process this file with autoconf to produce a configure script.
AC_INIT(INSTALL)
AM_INIT_AUTOMAKE(gale,0.90b)
AM_CONFIG_HEADER(include/gale/config.h:config.h.in)
AC_CANONICAL_HOST

dnl Use libtool for shared libraries
AM_PROG_LIBTOOL

AC_ARG_ENABLE(gzgw,
[  --enable-gzgw           Build the Zephyr gateway (requires Zephyr)],
[case "${enableval}" in
  yes) build_gzgw=true ;;
  no)  build_gzgw=false ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --enable-gzgw) ;;
esac],[build_gzgw=false])

AC_ARG_WITH(socks,
[  --with-socks            Use SOCKS firewall proxy (requires libsocks)],
[case "${withval}" in
  yes) use_socks=true ;;
  no)  use_socks=false ;;
  *) AC_MSG_ERROR(bad value ${withval} for --with-socks) ;;
esac],[use_socks=false])

dnl Configure subpackages
AC_CONFIG_SUBDIRS(liboop)

no_wacky_libs=""
GALE_LIBS="\${top_builddir}/lib/libgale.la"

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_YACC
PROG_LDCONFIG=:

dnl System type checks.
case "$host" in
  *-linux-*)
    AC_PATH_PROG(PROG_LDCONFIG, ldconfig, :, $PATH:/usr/sbin:/sbin)
    ;;
  *-sgi-irix6*)
    if test -n "$LPATH" ; then
      LDFLAGS="-Wl,-rpath,$LPATH $LDFLAGS"
    fi
    no_wacky_libs=yes
    AC_DEFINE(OS_IRIX)
    ;;
  *-hp-hpux*)
    AC_DEFINE(OS_HPUX)
    ;;
  *-*-solaris2*)
    AC_DEFINE(OS_SOLARIS)
    ;;
  *-*-*bsd*)
    AC_DEFINE(OS_BSD)
    ;;
esac

dnl Checks for libraries.
if test -z "$no_wacky_libs" ; then
  AC_CHECK_LIB(nsl,main)
  AC_CHECK_LIB(socket,socket)
fi

AC_CHECK_LIB(termcap,tgetent,[
  GALE_LIBS="$GALE_LIBS -ltermcap"
],[
  AC_CHECK_LIB(ncurses,tgetent,[GALE_LIBS="$GALE_LIBS -lncurses"])
])

AC_CHECK_LIB(dl,dlopen,[
  DL_LIBS="-ldl"
  AC_DEFINE(HAVE_LIBDL)])
AC_CHECK_LIB(readline,readline,[
  READLINE_LIBS="-lreadline"
  AC_DEFINE(HAVE_LIBREADLINE)])

if test x$use_socks = xtrue ; then
AC_CHECK_LIB(socks,SOCKSinit,[
  GALE_LIBS="$GALE_LIBS -lsocks"
  AC_CHECK_LIB(resolv,res_query,[GALE_LIBS="$GALE_LIBS -lresolv"])
  AC_DEFINE(HAVE_SOCKS) 
],[
  AC_MSG_ERROR(cannot find SOCKS library)
],-lresolv)
fi

if test x$build_gzgw = xtrue ; then
AC_CHECK_LIB(zephyr,ZInitialize,[
  ZEPHYR_LIBS="-lzephyr -lcom_err"
  AC_CHECK_LIB(krb,tf_get_pname,[ZEPHYR_LIBS="-lkrb $ZEPHYR_LIBS"])
  AC_CHECK_LIB(des,des_key_sched,[ZEPHYR_LIBS="-ldes $ZEPHYR_LIBS"])
],[
  AC_MSG_ERROR(cannot find library to build gzgw as specified)
],-lcom_err)
fi

AC_MSG_CHECKING([for local copy of Boehm GC])
if test -f "${srcdir}/gc/alloc.c"; then
  AC_MSG_RESULT(yes)
  if test ! -f "gc/Makefile.gc" ; then
    mkdir -p gc
    if test -f "${srcdir}/gc/Makefile" ; then
      cp "${srcdir}/gc/Makefile" "gc/Makefile.gc"
    elif test -f "${srcdir}/gc/Makefile.gc" ; then
      cp "${srcdir}/gc/Makefile.gc" "gc/Makefile.tmp"
      mv "gc/Makefile.tmp" "gc/Makefile.gc"
    else
      AC_MSG_ERROR(cannot find Makefile for gc)
    fi
    rm -f "gc/Makefile"
  fi
  CPPFLAGS="-I\${top_srcdir}/gc/include $CPPFLAGS"
  GALE_LIBS="$GALE_LIBS \${top_builddir}/gc/libgc.a"
  build_gc=true
else
  AC_MSG_RESULT(no)
  AC_CHECK_LIB(gc,GC_malloc,[:],[
    AC_MSG_ERROR(cannot find Boehm GC library, see INSTALL directions)
  ])
  GALE_LIBS="$GALE_LIBS -lgc"
fi

AC_MSG_CHECKING([for local copy of RSAREF source])
if test -f "${srcdir}/rsaref/source/rsa.c"; then
  AC_MSG_RESULT(yes)
  CPPFLAGS="-I\${top_srcdir}/rsaref/source $CPPFLAGS"
  build_rsaref=true
  AC_CHECK_FUNCS(MD5Init MD5Update MD5Final,[
    dnl Some operating systems define these functions themselves; they're
    dnl not reliably equivalent.  We demand the real RSAREF!
    CPPFLAGS="-DMD5Init=gMD5Init -DMD5Update=gMD5Update -DMD5Final=gMD5Final $CPPFLAGS"
    break
  ])
  GALE_LIBS="$GALE_LIBS \${top_builddir}/rsaref/source/librsaref.la"
else
  AC_MSG_RESULT(no)
  AC_CHECK_LIB(rsaref,R_RandomInit,[:],[
    AC_MSG_ERROR(cannot find RSAREF library, see INSTALL directions)
  ])
  GALE_LIBS="$GALE_LIBS -lrsaref"
fi

AC_MSG_CHECKING([for local copy of liboop])
if test -f "${srcdir}/liboop/oop.h"; then
  AC_MSG_RESULT(yes)
  CPPFLAGS="-I\${top_srcdir}/liboop $CPPFLAGS"
  AC_CHECK_LIB(adns,adns_init,[
    GALE_LIBS="$GALE_LIBS \${top_builddir}/liboop/liboop-adns.la -ladns"
    AC_DEFINE(HAVE_ADNS)
  ])
  GALE_LIBS="$GALE_LIBS \${top_builddir}/liboop/liboop.la"
else
  AC_MSG_RESULT(no)
  AC_CHECK_LIB(oop,oop_sys_new,[:],[
    AC_MSG_ERROR(cannot find liboop library, see INSTALL directions)
  ])
  AC_CHECK_LIB(adns,adns_init,[
    GALE_LIBS="$GALE_LIBS -loop-adns -ladns"
    AC_DEFINE(HAVE_ADNS)
  ])
  GALE_LIBS="$GALE_LIBS -loop"
fi

dnl Checks for header files.
AC_CHECK_HEADERS(sys/bitypes.h sys/select.h curses.h term.h dlfcn.h readline/readline.h getopt.h)

if test ! -f "${srcdir}/rsaref/source/rsaref.h"; then
  AC_CHECK_HEADERS(rsaref.h,,[
    AC_MSG_ERROR(cannot find RSAREF headers, see INSTALL directions)
  ])
fi

if test ! -f "${srcdir}/gc/include/gc.h"; then
  AC_CHECK_HEADERS(gc.h,,[
    AC_MSG_ERROR(cannot find Boehm GC headers, see INSTALL directions)
  ])
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(long)

dnl Checks for library functions.

dnl Output.

INCLUDES="-I\${top_srcdir}/include -I\${top_builddir}/include"
INCLUDES="-DGALE_SYS_DIR=\\\"${sysconfdir}/gale\\\" $INCLUDES"

AC_SUBST(INCLUDES)
AC_SUBST(ZEPHYR_LIBS)
AC_SUBST(READLINE_LIBS)
AC_SUBST(DL_LIBS)
AC_SUBST(GALE_LIBS)
AC_SUBST(PROG_LDCONFIG)

AM_CONDITIONAL(BUILD_GZGW,test x$build_gzgw = xtrue)
AM_CONDITIONAL(BUILD_RSAREF,test x$build_rsaref = xtrue)
AM_CONDITIONAL(BUILD_GC,test x$build_gc = xtrue)

AC_OUTPUT(
	gale-config
	gale-install
	Makefile 
	gc/Makefile
	gc/include/Makefile
	rsaref/Makefile
	rsaref/source/Makefile
	lib/Makefile 
	gsub/Makefile
	gzgw/Makefile
	gsend/Makefile
	kutils/Makefile
	gwatch/Makefile
	server/Makefile
	gdomain/Makefile
	include/Makefile 
	include/gale/Makefile
)
